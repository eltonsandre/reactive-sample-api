plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.4.3'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
}

apply from: './sonar.gradle'

group = 'com.github.eltonsandre'
version = '0.0.1-SNAPSHOT'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenLocal()
    mavenCentral()
}

ext {
    set('springCloudVersion', "2020.0.1")
    set('testcontainersVersion', "1.15.1")
    set('mapstructVersion', '1.4.1.Final')
}

dependencies {
//    implementation 'reactor.cache:reactor-cache-spring:1.0.0'

    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    ['compileOnly', 'annotationProcessor', 'testCompileOnly', 'testAnnotationProcessor'].each {
        conf -> add(conf, 'org.projectlombok:lombok:1.18.18')
    }

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor',
            "org.mapstruct:mapstruct-processor:$mapstructVersion"

    implementation "org.mapstruct:mapstruct:$mapstructVersion"

    implementation 'org.springframework.boot:spring-boot-starter-webflux',
            'org.springframework.boot:spring-boot-starter-validation',
            'org.springframework.boot:spring-boot-starter-actuator',
            'org.springframework.cloud:spring-cloud-starter-sleuth',
            'org.springframework.boot:spring-boot-starter-aop',
            'org.springdoc:springdoc-openapi-webflux-ui:1.5.1',
            'io.projectreactor.addons:reactor-extra'

    implementation 'org.springframework.boot:spring-boot-starter-data-mongodb',
            'org.springframework.boot:spring-boot-starter-data-mongodb-reactive'

    implementation 'org.springframework.boot:spring-boot-starter-data-redis',
            'org.springframework.boot:spring-boot-starter-data-redis-reactive'

    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    testImplementation 'org.springframework.boot:spring-boot-starter-test',
            'io.projectreactor:reactor-test',
            'org.testcontainers:junit-jupiter',
            'org.testcontainers:mongodb'

//    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'com.github.spotbugs:spotbugs:4.2.3'


}

dependencyManagement {
    imports {
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

test {
    useJUnitPlatform()
}

clean.doFirst {
    delete 'build', 'out'
}

sonarqube {
    properties {
        property "sonar.findbugs.excludesFilters", "findbugs-exclude.xml"
    }
}
